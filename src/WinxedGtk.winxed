// Pruebas gtk


//**********************************************************************

namespace WinxedGtk
{

const int DEBUG = 0;

namespace __private
{

$include_const 'datatypes.pasm';

function getlib()
{
    var lib;

    for (string libname in [
            'libgtk-x11',
            'libgtk-x11-2.0',
            'libgtk-x11-2.0.so',
            '/usr/lib/libgtk-x11-2.0.so',
            '/usr/lib/libgtk-x11-2.0.so.0'
            ] ) {
        lib = loadlib(libname);
        if (lib) {
            if (DEBUG) cry("Loaded library: '", libname, "'");
            break;
        }
    }
    if (lib == null)
        throw "Cannot load lib";

    for (;;)
        yield lib;
}

function get_parrot_func(string name, string signature)
{
    return dlfunc(null, name, signature);
}

function parrot_str_to_cstring()
{
    var func = get_parrot_func('Parrot_str_to_cstring',   'ppS');
    for (;;)
        yield func;
}

function parrot_str_free_cstring()
{
    var func = get_parrot_func('Parrot_str_free_cstring',   'vp');
    for (;;)
        yield func;
}

function parrot_str_from_platform_cstring()
{
    var func = get_parrot_func('Parrot_str_from_platform_cstring',   'Spp');
    for (;;)
        yield func;
}

function str_to_cstring(string s)
{
    return parrot_str_to_cstring()(getinterp(), s);
}

function str_free_cstring(s)
{
    parrot_str_free_cstring()(s);
}

function string_from_nci(p)
{
    return parrot_str_from_platform_cstring()(getinterp(), p);
}

function gtk_main_level()
{
    var func = dlfunc(getlib(), "gtk_main_level", "i");
    int i = func();
    return i;
}

function mainloop_quit()
{
    var quit = false;
    for (;;) yield quit;
}

function __text_iter_new()
{
    // There is no function to allocate a new GtkTextIter,
    // this does the trick.
    var iter_copy = dlfunc(getlib(), "gtk_text_iter_copy", "pp");
    var ugly = new ['ManagedStruct'](256);
    return iter_copy(ugly);
}

function __text_iter_free(iter)
{
    var iter_free = dlfunc(getlib(), "gtk_text_iter_free", "vp");
    iter_free(iter);
}

function __check_version()
{
    var retfunc;
    var func = dlfunc(getlib(), "gtk_check_version", "piii");
    if (func == null)
        retfunc = function (int major, int minor, int micro)
        {
            return "Cannot check gtk version";
        };
    else retfunc = function (int major, int minor, int micro)
        {
            var p = func(major, minor, micro);
            string result;
            if (p != null)
                result = string_from_nci(p);
            return result;
        };
    for (;;) yield retfunc;
}

function __widget_get_window()
{
    var retfunc;
    var func = dlfunc(getlib(), "gtk_widget_get_window", "pp");
    if (func != null)
        retfunc = function (gtkw)
        {
            var gdkwindow = func(gtkw);
            return gdkwindow;
        };
    else
        retfunc = function (gtkw)
        {
            // The function gtk_widget_get_window is not available
            // until Gtk+ 2.14
            // This is a veeeery convoluted way to get
            // the window property from the widget.
            var interp = getinterp();
            var pointee = new['ManagedStruct']([DATATYPE_INT, 0, 0]);
            var pointer = new ['Pointer'];
            var get = get_parrot_func("Parrot_PMC_get_pointer", "ipP");
            var set = get_parrot_func("Parrot_PMC_set_pointer", "vpPi");
            if (get == null || set == null) throw "Vaya";
            int pval = get(interp, pointee);
            set(interp, pointer, pval);
            var func = dlfunc(getlib(), "g_object_get", "vpppp");
            var p = str_to_cstring('window');
            func(gtkw, p, pointer, null);
            int n = pointee[0];
            var result = new ['UnManagedStruct'];
            set(interp, result, n);
            return result;
        };
    for (;;) yield retfunc;
}

function __widget_get_style()
{
    var retfunc;
    var func = dlfunc(getlib(), "gtk_widget_get_style", "pp");
    if (func != null)
        retfunc = function (gtkw)
        {
            var gdkstyke = func(gtkw);
            return gdkstyke;
        };
    else
        retfunc = function (gtkw)
        {
            throw "Can't draw with this gtk";
        };
    for (;;) yield retfunc;
}

} // namespace __private

//**********************************************************************

using namespace __private;

function init(args)
{
    int argc = elements(args);
    var pargs = new 'ManagedStruct'([ DATATYPE_CSTR, argc, 0]);
    for (int i = 0; i < argc; ++i)
        pargs[0,i] = string(args[i]);
    var pargc = new 'ManagedStruct'([ DATATYPE_PTR, 0, 0]);
    var func = dlfunc(getlib(), "gtk_init", "ipp");
    func(pargc, pargs);
}

function gtk_main()
{
    var func = dlfunc(getlib(), "gtk_main_iteration", "i");
    int i;
    var quit = mainloop_quit();
    do i = func(); while (i && ! quit);
}

function gtk_main_quit()
{
    int i = gtk_main_level();
    if (i == 0)
        mainloop_quit() =: true;
    else {
        var func = dlfunc(getlib(), "gtk_main_quit", "v");
        func();
    }
}

function gtk_check_version(int major, int minor, int micro)
{
    return __check_version()(major, minor, micro);
}

//**********************************************************************

class GObject
{
    var gtkw;
    function __gtkset(gtkw)
    {
        self.gtkw = gtkw;
    }
}

class Widget : GObject
{
    function destroy()
    {
        var func = dlfunc(getlib(), "gtk_widget_destroy", "vp");
        func(self.gtkw);
    }
    function show()
    {
        var func = dlfunc(getlib(), "gtk_widget_show", "vp");
        func(self.gtkw);
    }
    function set_size_request(int width, int height)
    {
        var func = dlfunc(getlib(), "gtk_widget_set_size_request", "vpii");
        func(self.gtkw, width, height);
    }
    function signal_connect(string signal_name, callback,
            data[optional], int has_data[opt_flag])
    {
        var func = dlfunc(getlib(), "gtk_signal_connect_full", "lppppPpii");
        var callback_function = has_data ?
            function(widget, actualdata)
            {
                callback(actualdata);
            } :
            function(widget, actualdata)
            {
                callback();
            };
        var cb_data = has_data ? data : callback_function;
        var funback;
        ${ new_callback funback, callback_function, cb_data, 'iUp' };
        var signame = str_to_cstring(signal_name);
        if (DEBUG) cry("connecting signal '", signal_name, "'");
        int r = func(self.gtkw, signame, funback, null, cb_data, null, 1, 1);
        str_free_cstring(signame);
        return r;
    }
    function signal_disconnect(handler_id)
    {
        var func = dlfunc(getlib(), "g_signal_handler_disconnect", "vpl");
        func(self.gtkw, handler_id);
    }
}

class Misc : Widget
{
    function set_alignment(float xalign, float yalign)
    {
        var func = dlfunc(getlib(), "gtk_misc_set_alignment", "vpff");
        func(self.gtkw, xalign, yalign);
    }
    function set_padding(float xpad, float ypad)
    {
        var func = dlfunc(getlib(), "gtk_misc_set_padding", "vpff");
        func(self.gtkw, xpad, ypad);
    }
}

class Label : Misc
{
    function Label(string text)
    {
        var func = dlfunc(getlib(), "gtk_label_new", "pp");
        var p = str_to_cstring(text);
        self.__gtkset(func(p));
        str_free_cstring(p);
    }
}

class Editable : Widget {
    function get_chars(int start_pos, int end_pos)
    {
        var func = dlfunc(getlib(), "gtk_editable_get_chars", "ppii");
        var p = func(self.gtkw, start_pos, end_pos);
        string r;
        if (p != null)
            r = string_from_nci(p);
        //TODO: free the GtkEditable allocated string
        return r;
    }
}

class Entry : Editable
{
    function Entry()
    {
        var func = dlfunc(getlib(), "gtk_entry_new", "p");
        self.__gtkset(func());
    }
    function get_text()
    {
        var func = dlfunc(getlib(), "gtk_entry_get_text", "pp");
        var t = func(self.gtkw);
        string result;
        if (t != null) {
            result = string_from_nci(t);
            if (DEBUG) cry("Entry.get_text - got '", result, "'");
        }
        return result;
    }
}

class Text : Editable
{
    function Text()
    {
        var func = dlfunc(getlib(), "gtk_text_new", "ppp");
        self.__gtkset(func(null, null));
    }
    function set_editable(int editable)
    {
        var func = dlfunc(getlib(), "gtk_text_set_editable", "vpi");
        func(self.gtkw, editable);
    }
    function insert(string text)
    {
        var func = dlfunc(getlib(), "gtk_text_insert", "vpppppi");
        var p = str_to_cstring(text);
        func(self.gtkw, null, null, null, p, -1);
        str_free_cstring(p);
    }
}

class Container : Widget
{
    function set_border_width(int width)
    {
        var func = dlfunc(getlib(), "gtk_container_set_border_width", "vpi");
        func(self.gtkw, width);
    }
    function add(item)
    {
        var func = dlfunc(getlib(), "gtk_container_add", "vpp");
        func(self.gtkw, item.gtkw);
    }
}

class TextIter
{
    var gtkiter;
    function TextIter()
    {
        self.gtkiter = __text_iter_new();
    }
}

class TextBuffer : GObject
{
    function TextBuffer()
    {
        var func = dlfunc(getlib(), "gtk_text_buffer_new", "p");
        self.__gtkset(func());
    }
    function get_char_count()
    {
        var func = dlfunc(getlib(), "gtk_text_buffer_get_char_count", "ip");
        return func(self.gtkw);
    }
    function get_start_iter()
    {
        var func = dlfunc(getlib(), "gtk_text_buffer_get_start_iter", "vpp");
        var textiter = new TextIter();
        func(self.gtkw, textiter.gtkiter);
        return textiter;
    }
    function get_end_iter()
    {
        var func = dlfunc(getlib(), "gtk_text_buffer_get_end_iter", "vpp");
        var textiter = new TextIter();
        func(self.gtkw, textiter.gtkiter);
        return textiter;
    }
    function get_text(start, end, int include_hidden_chars)
    {
        var func = dlfunc(getlib(), "gtk_text_buffer_get_text", "ppppi");
        var p = func(self.gtkw, start.gtkiter, end.gtkiter, include_hidden_chars);
        return string_from_nci(p);
    }
    function set_text(string text)
    {
        //TODO reencode text if not already utf8
        var func = dlfunc(getlib(), "gtk_text_buffer_set_text", "vppi");
        int len = bytelength(text);
        var p = str_to_cstring(text);
        func(self.gtkw, p, len);
        str_free_cstring(p);
    }
}

class TextView : Container
{
    function TextView()
    {
        var func = dlfunc(getlib(), "gtk_text_view_new", "p");
        self.__gtkset(func());
    }
    function set_editable(int editable)
    {
        var func = dlfunc(getlib(), "gtk_text_view_set_editable", "vpi");
        func(self.gtkw, editable);
    }
    function __get_buffer()
    {
        var func = dlfunc(getlib(), "gtk_text_view_get_buffer", "pp");
        return func(self.gtkw);
    }
    function get_buffer()
    {
        var textbuffer = new TextBuffer; // Don't call the constructor
        textbuffer.__gtkset(self.__get_buffer());
        return textbuffer;
    }
    function get_char_count()
    {
        return self.get_buffer().get_char_count();
    }
    function get_text()
    {
       var buffer = self.get_buffer();
       return buffer.get_text(buffer.get_start_iter(), buffer.get_end_iter(), 0);
    }
}

class Bin : Container
{
}

class Box : Container
{
    function pack_start(widget, int expand, int fill, int padding)
    {
        var func = dlfunc(getlib(), "gtk_box_pack_start", "vppiii");
        func(self.gtkw, widget.gtkw, expand, fill, padding);
    }
}

class VBox : Box
{
    function VBox(int homogeneous, int spacing)
    {
        var func = dlfunc(getlib(), "gtk_vbox_new", "pii");
        self.__gtkset(func(homogeneous, spacing));
    }
}

class HBox : Box
{
    function HBox(int homogeneous, int spacing)
    {
        var func = dlfunc(getlib(), "gtk_hbox_new", "pii");
        self.__gtkset(func(homogeneous, spacing));
    }
}

class ScrolledWindow : Bin
{
    function ScrolledWindow()
    {
        var func = dlfunc(getlib(), "gtk_scrolled_window_new", "ppp");
        self.__gtkset(func(null, null));
    }
    function add_with_viewport(child)
    {
        var func = dlfunc(getlib(), "gtk_scrolled_window_add_with_viewport", "vpp");
        func(self.gtkw, child.gtkw);
    }
    function set_policy(int hpolicy, int vpolicy)
    {
        var func = dlfunc(getlib(), "gtk_scrolled_window_set_policy", "vpii");
        func(self.gtkw, hpolicy, vpolicy);
    }
}

class Window : Bin
{
    function Window(int level)
    {
        var func = dlfunc(getlib(), "gtk_window_new", "pi");
        self.__gtkset(func(0));
    }
    function set_title(string title)
    {
        var func = dlfunc(getlib(), "gtk_window_set_title", "vpp");
        var ptitle = str_to_cstring(title);
        func(self.gtkw, ptitle);
        str_free_cstring(ptitle);
    }
}

class Dialog : Window
{
    function run()
    {
        var func = dlfunc(getlib(), "gtk_dialog_run", "ip");
        int r = func(self.gtkw);
        return r;
    }
}

class MessageDialog : Dialog
{
    function MessageDialog(parent, int flags, int type, int buttons, string message)
    {
        var func = dlfunc(getlib(), "gtk_message_dialog_new", "ppiiipp");
        var format = str_to_cstring("%s");
        var p = str_to_cstring(message);
        self.__gtkset(func(parent.gtkw, flags, type, buttons, format, p));
        str_free_cstring(format);
        str_free_cstring(p);
    }
}

class Button : Bin
{
    function Button(string label)
    {
        var func = dlfunc(getlib(), "gtk_button_new_with_label", "pp");
        var p = str_to_cstring(label);
        self.__gtkset(func(p));
        str_free_cstring(p);
    }
}

class TreeItem : Widget
{
    function TreeItem(string label)
    {
        var func = dlfunc(getlib(), "gtk_tree_item_new_with_label", "pp");
        var p = str_to_cstring(label);
        self.__gtkset(func(p));
        str_free_cstring(p);
    }
    function set_subtree(tree)
    {
        var func = dlfunc(getlib(), "gtk_tree_item_set_subtree", "vpp");
        func(self.gtkw, tree.gtkw);
    }
}

class Tree : Widget
{
    function Tree()
    {
        var func = dlfunc(getlib(), "gtk_tree_new", "p");
        self.__gtkset(func(0));
    }
    function append(item)
    {
        var func = dlfunc(getlib(), "gtk_tree_append", "vpp");
        func(self.gtkw, item.gtkw);
    }
}

class CList : Widget
{
    function CList(int ncolumns)
    {
        var func = dlfunc(getlib(), "gtk_clist_new", "pi");
        self.__gtkset(func(ncolumns));
    }
    function column_titles_show()
    {
        var func = dlfunc(getlib(), "gtk_clist_column_titles_show", "vp");
        func(self.gtkw);
    }
    function set_column_title(int column, string title)
    {
        var func = dlfunc(getlib(), "gtk_clist_set_column_title", "vpip");
        var p = str_to_cstring(title);
        func(self.gtkw, column, p);
        str_free_cstring(p);
    }
    function set_text(int row, int column, string text)
    {
        var func = dlfunc(getlib(), "gtk_clist_set_text", "vpiip");
        var p;
        if (text != null)
            p = str_to_cstring(text);
        func(self.gtkw, row, column, p);
        str_free_cstring(p);
    }
    function append(row)
    {
        int n = elements(row);
        var data = new 'ManagedStruct'([ DATATYPE_CSTR, n, 0]);
        for (int i = 0; i < n; ++i) {
            string r = row[i];
            data[0,i] = r;
        }
        var func = dlfunc(getlib(), "gtk_clist_append", "vpp");
        func(self.gtkw, data);
    }
}

class Image : Misc
{
    function Image(string filename)
    {
        var func = dlfunc(getlib(), "gtk_image_new_from_file", "pp");
        var p = str_to_cstring(filename);
        self.__gtkset(func(p));
        str_free_cstring(p);
    }
}

class DrawingArea : Widget
{
    function DrawingArea()
    {
        var func = dlfunc(getlib(), "gtk_drawing_area_new", "p");
        self.__gtkset(func());
    }
    function draw_rectangle(int x, int y, int width, int height)
    {
        var gdkwindow = __widget_get_window()(self.gtkw);
        var style = __widget_get_style()(self.gtkw);
        var func = dlfunc(getlib(), "gtk_draw_box", "vppiiiiii");
        func(style, gdkwindow, 0, 0, x, y, width, height);
    }
}

class MenuItem : Bin
{
    function MenuItem(string label)
    {
        var func = dlfunc(getlib(), "gtk_menu_item_new_with_label", "pp");
        var p = str_to_cstring(label);
        self.__gtkset(func(p));
        str_free_cstring(p);
    }
    function set_submenu(submenu)
    {
        var func = dlfunc(getlib(), "gtk_menu_item_set_submenu", "vpp");
        func(self.gtkw, submenu.gtkw);
    }
}

class MenuShell : Container
{
    function append(item)
    {
        var func = dlfunc(getlib(), "gtk_menu_shell_append", "vpp");
        func(self.gtkw, item.gtkw);
    }
}

class MenuBar : MenuShell
{
    function MenuBar()
    {
        var func = dlfunc(getlib(), "gtk_menu_bar_new", "p");
        self.__gtkset(func());
    }
}

class Menu : MenuShell
{
    function Menu()
    {
        var func = dlfunc(getlib(), "gtk_menu_new", "p");
        self.__gtkset(func());
    }
}

} // namespace WinxedGtk

//**********************************************************************

// End

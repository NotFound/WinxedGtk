// Pruebas gtk

$load 'WinxedGtk.pbc';

//**********************************************************************

namespace WinxedGtk
{

class VBox;
class HBox;
class Window;
class Label;
class Button;
class Entry;
class Text;
class Image;
class Tree;
class TreeItem;

}

using namespace WinxedGtk;

function show(widget)
{
    widget.show();
    return widget;
}

class SimpleTestWindow : Window
{
    var entry;
    function SimpleTestWindow()
    {
        self.Window(0);
        self.set_border_width(20);
        self.set_title('WinxedGtk simple test');
        self.signal_connect('destroy',
            function ()
            {
                using WinxedGtk.gtk_main_quit;
                gtk_main_quit();
            }
        );
        var vbox = new VBox(0, 4);


        var image = new Image('parrot_small.png');
        image.show();

        self.entry = new Entry();
        self.entry.show();
        var hbox = new HBox(1, 4);

        var tree = new Tree();
        tree.show();
        var item2 = new TreeItem('item');
        tree.append(item2);
        item2.show();
        var subtree = new Tree();
        var subitem = new TreeItem('subitem');
        subtree.append(subitem);
        item2.set_subtree(subtree);
        subtree.show();
        subitem.show();
        item2.show();

        var text = new Text();
        text.insert('Some text...');
        text.set_editable(true);
        text.show();

        var button = new Button('say hello');
        button.signal_connect('clicked',
            function ()
            {
                string t = self.entry.get_text();
                if (t != null && length(t) > 0)
                    say('hello, ', t);
                else
                    say('hello');
                t = text.get_chars(0, -1);
                if (t != null && t != '')
                    say("Here is some text:\n", t);
            }
        );
        var buttonclose = new Button('close');
        buttonclose.signal_connect('clicked',
            function ()
            {
                self.destroy();
            }
        );
        button.show();
        buttonclose.show();
        hbox.pack_start(button, 0, 0, 0);
        hbox.pack_start(buttonclose, 0, 0, 0);

        vbox.add(show(new Label('This is a simple Window example')));
        vbox.add(image);
        vbox.add(show(new Label('A Tree widget')));
        vbox.add(tree);
        vbox.add(show(new Label('A Entry widget')));
        vbox.add(self.entry);
        vbox.add(show(new Label('A Text widget')));
        vbox.add(text);

        vbox.pack_start(hbox, 0, 0, 0);

        self.add(vbox);
        hbox.show();
        vbox.show();
    }
}

function main(args)
{
    using WinxedGtk.init;
    using WinxedGtk.gtk_main;

    say('Initializing');
    init(args);

    var window = new SimpleTestWindow();
    window.show();
    say('Running');
    gtk_main();

    say('End');
}

// End

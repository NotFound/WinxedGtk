// Pruebas gtk

$load 'WinxedGtk.pbc';

//**********************************************************************

namespace WinxedGtk
{

class VBox;
class HBox;
class Window;
class Label;
class Button;
class Entry;
class TextView;
class Image;
class DrawingArea;
class Tree;
class TreeItem;
class MenuItem;
class MenuBar;
class Menu;

}

using namespace WinxedGtk;

function show(widget)
{
    widget.show();
    return widget;
}

class SimpleTestWindow : Window
{
    var entry;
    function SimpleTestWindow()
    {
        self.Window(0);
        self.set_border_width(0);
        self.set_title('WinxedGtk simple test');
        self.signal_connect('destroy', function () { self.exit(); } );

        var mainbox = new VBox(0, 0);
        var vbox = new VBox(0, 4);
        vbox.set_border_width(10);

        var itemexit = new MenuItem('exit');
        itemexit.show();
        itemexit.signal_connect('activate', function () { self.exit(); } );
        var menufile = new Menu();
        menufile.show();
        menufile.append(itemexit);
        var itemfile = new MenuItem('File');
        itemfile.show();
        itemfile.set_submenu(menufile);

        var submenuitem = new MenuItem('subtest');
        var submenu = new Menu();
        submenu.append(submenuitem);
        var item = new MenuItem('test');
        var menu = new MenuBar();
        menu.append(itemfile);
        menu.append(item);
        item.set_submenu(submenu);
        mainbox.pack_start(menu, false, false, 0);
        mainbox.add(vbox);
        mainbox.show();
        self.add(mainbox);

        submenuitem.show();
        item.show();
        submenu.show();
        menu.show();
        submenuitem.signal_connect('activate',
            function ()
            {
                say('submenu item activated');
            }
        );

        var image = new Image('parrot_small.png');
        image.show();

        self.entry = new Entry();
        self.entry.show();
        var hbox = new HBox(1, 4);

        var tree = new Tree();
        tree.show();
        var item2 = new TreeItem('item');
        tree.append(item2);
        item2.show();
        var subtree = new Tree();
        var subitem = new TreeItem('subitem');
        subtree.append(subitem);
        item2.set_subtree(subtree);
        subtree.show();
        subitem.show();
        item2.show();

        var text = new TextView();
        //text.insert('Some text...');
        text.set_editable(true);
        text.show();

        var drawing;
        using WinxedGtk.gtk_check_version;
        string check = gtk_check_version(2, 14, 0);
        if (check != null)
            cry("Omiting DrawingArea test: " + check);
        else {
            drawing = new DrawingArea();
            drawing.set_size_request(600, 400);
            drawing.signal_connect('expose_event',
                function ()
                {
                    cry('Exposed');
                    drawing.draw_rectangle(100, 100, 400, 100);
                }
            );
            drawing.show();
        }

        var button = new Button('say hello');
        button.signal_connect('clicked',
            function ()
            {
                string t = self.entry.get_text();
                if (t != null && length(t) > 0)
                    say('hello, ', t);
                else
                    say('hello');
                if (text.get_char_count() > 0) {
                    t = text.get_text();
                    say("Here is some text:\n", t);
                }
            }
        );
        var buttonclose = new Button('close');
        buttonclose.signal_connect('clicked',
            function ()
            {
                self.destroy();
            }
        );
        button.show();
        buttonclose.show();
        hbox.pack_start(button, 0, 0, 0);
        hbox.pack_start(buttonclose, 0, 0, 0);

        vbox.add(show(new Label('This is a simple Window example')));
        vbox.add(image);
        vbox.add(show(new Label('A Tree widget')));
        vbox.add(tree);
        vbox.add(show(new Label('A Entry widget')));
        vbox.add(self.entry);
        vbox.add(show(new Label('A Text widget')));
        vbox.add(text);
        if (drawing != null) {
            vbox.add(show(new Label('A DrawingArea widget')));
            vbox.add(drawing);
        }

        vbox.pack_start(hbox, 0, 0, 0);

        //self.add(vbox);
        hbox.show();
        vbox.show();

    }
    function exit()
    {
        using WinxedGtk.gtk_main_quit;
        gtk_main_quit();
    }
}

function main(args)
{
    using WinxedGtk.init;
    using WinxedGtk.gtk_main;

    say('Initializing');
    init(args);

    var window = new SimpleTestWindow();
    window.show();
    say('Running');
    gtk_main();

    say('End');
}

// End

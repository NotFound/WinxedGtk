#! winxed

// JSON tree view - Pruebas WinxedGtk

$load 'WinxedGtk.pbc';

$include 'WinxedGtk.winxhead';

//**********************************************************************

using namespace WinxedGtk;

// Set to 1 to compile a debug version.
const int DEBUG = 0;

class JsonTreeWindow : Window
{
    function JsonTreeWindow(string filename, data)
    {
        self.Window(0);
        self.set_title('WinxedGtk tree view test');
        self.set_border_width(0);
        self.signal_connect('destroy',
            function ()
            {
                using WinxedGtk.gtk_main_quit;
                gtk_main_quit();
            }
        );
        var mainbox = new VBox(0, 0);
        mainbox.show();
        var itemquit = new MenuItem('quit');
        itemquit.signal_connect('activate',
            function () { self.destroy(); } );
        itemquit.show();
        var menubar = new MenuBar();
        menubar.show();
        var itemfile = new MenuItem('File');
        itemfile.show();
        var menufile = new Menu();
        menufile.append(itemquit);
        menufile.show();
        itemfile.set_submenu(menufile);
        menubar.append(itemfile);
        mainbox.pack_start(menubar, false, false, 0);

        var vbox = new VBox(0, 4);
        vbox.set_border_width(8);
        var label = new Label('JSON tree view');
        var label2 = new Label(filename);

        var image = new Image('parrot_small.png');
        image.show();

        label.show();
        label2.show();
        var hbox = new HBox(1, 4);

        var tree = new Tree();
        add_object(tree, data);
        tree.show();

        var buttonclose = new Button('close');
        buttonclose.signal_connect('clicked',
            function () { self.destroy(); } );
        buttonclose.show();
        hbox.pack_start(buttonclose, 0, 0, 0);

        vbox.pack_start(image, 0, 0, 4);
        vbox.pack_start(label, 0, 0, 10);

        var scrolled = new ScrolledWindow();
        scrolled.add_with_viewport(tree);
        scrolled.set_policy(1, 1);
        vbox.pack_start(scrolled, true, true, 0);
        scrolled.show();

        vbox.pack_start(label2, 0, 0, 6);
        vbox.pack_start(hbox, 0, 0, 6);

        mainbox.add(vbox);
        self.add(mainbox);
        hbox.show();
        vbox.show();
    }
}

function add_object(tree, object)
{
    var item;
    switch {
      case object instanceof ['ResizablePMCArray']:
        int n = elements(object);
        for (int i = 0; i < n; ++i) {
            item = new TreeItem(string(i));
            var subtree = new Tree();
            add_object(subtree, object[i]);
            tree.append(item);
            subtree.show();
            item.set_subtree(subtree);
            item.show();
        }
        break;
      case object instanceof ['Hash']:
        for (string key in object) {
            item = new TreeItem(key);
            var subtree = new Tree();
            add_object(subtree, object[key]);
            tree.append(item);
            subtree.show();
            item.set_subtree(subtree);
            item.show();
        }
        break;
      default:
        string str = object;
        item = new TreeItem(str);
        tree.append(item);
        item.show();
    }
}

function read_data(string filename, string startmark, string endmark)
{
    var json = load_language('data_json');
    var text;
    if (substr(filename, 0, 7) == "http://") {
        using extern LWP.UserAgent;
        var ua = new ['LWP', 'UserAgent'];
        var nargs = {
            'Connection' : 'close'
        };
        var resp = ua.get(filename, nargs:[named,flat]);
        int code = resp.code;
        if (code != 200)
            throw "Failed to load from url: " + string(code);
        // LWP currently does not provide a way to automatically apply
        // the encoding, thus do it here.
        var b = new ['ByteBuffer'];
        b =: string(resp.content);
        text = b.get_string('utf8');
    }
    else {
        var file = open(filename);
        file.encoding('utf8');
        text = file.readall();
        file.close();
    }

    if (startmark != null) {
        string mark = "\n" + startmark + "\n";
        int pos = indexof(text, mark);
        if (pos >= 0) {
            int cutinitpos = pos + length(mark);
            if (endmark == null)
                text = substr(text, cutinitpos);
            else {
                mark = "\n" + endmark + "\n";
                int cutlength;
                pos = indexof(text, mark, cutinitpos);
                if (pos >= 0)
                    text = substr(text, cutinitpos, pos - cutinitpos + 1);
                else
                    text = substr(text, cutinitpos);
            }
            if (DEBUG) cry('Filtered: "', text, '"');
        }
    }

    var code = json.compile(text);
    var data = code();
    return data;
}

function main(args)
{
    using WinxedGtk.init;
    using WinxedGtk.gtk_main;

    if (DEBUG) cry('Initializing Gtk');
    init(args);

    // Simplified way of getting command line args
    args.shift();
    :(string filename, string markbegin[optional], string markend[optional]) =
        function (a) { return a:[flat]; } (args);

    if (DEBUG) cry('Loading JSON');
    var data = read_data(filename, markbegin, markend);

    var window = new JsonTreeWindow(filename, data);

    if (DEBUG) cry('Running main loop');
    window.show();
    gtk_main();

    if (DEBUG) cry('End');
}

// End
